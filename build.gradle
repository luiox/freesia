import org.gradle.api.publish.maven.tasks.PublishToMavenRepository

plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.github.luiox'
version = '1.9'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    maven {
        url = uri('https://maven.aliyun.com/repository/public/')
    }
    mavenCentral()
    maven {
        url = uri('https://www.jitpack.io')
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.register('benchmarkTest', Test) {
    useJUnitPlatform()
    include '**/EventManagerPerformanceTest.class'
    systemProperty 'freesia.benchmark', 'true'
    testLogging {
        showStandardStreams = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name
            version = project.version.toString()
            from components.java

            pom {
                name = "${project.group}:${project.name}"
                description = 'A modern, high-performance, low-latency Java event bus'
                url = 'https://github.com/luiox/freesia'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/luiox/freesia/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'luiox'
                        name = 'Canrad'
                        email = '1517807724@qq.com'
                    }
                }
            }
        }
    }
}

tasks.withType(PublishToMavenRepository).configureEach {
    onlyIf {
        repository.name != 'MavenLocal' || project.hasProperty('publishToLocal')
    }
}